class Face : Any
{
    maide prusate Bool Init()
    {
        base.Init();
        var Extern extern;
        extern : share Extern;
        this.Extern : extern;
        
        var InternInfra internInfra;
        internInfra : share InternInfra;
        this.InternInfra : internInfra;

        var Int internFamily;
        internFamily : internInfra.StringCreate(this.Family);
        this.InternFamily : internFamily;

        var Int sizeU;
        sizeU : this.Size;
        var Int weightU;
        weightU : this.Weight;
        var Int italicU;
        italicU : internInfra.IntBool(this.Italic);
        var Int underlineU;
        underlineU : internInfra.IntBool(this.Underline);
        var Int overlineU;
        overlineU : internInfra.IntBool(this.Overline);
        var Int strikeoutU;
        strikeoutU : internInfra.IntBool(this.Strikeout);

        var Int intern;
        intern : extern.Face_New();
        this.Intern : intern;
        extern.Face_FamilySet(intern, internFamily);
        extern.Face_SizeSet(intern, sizeU);
        extern.Face_WeightSet(intern, weightU);
        extern.Face_ItalicSet(intern, italicU);
        extern.Face_UnderlineSet(intern, underlineU);
        extern.Face_OverlineSet(intern, overlineU);
        extern.Face_StrikeoutSet(intern, strikeoutU);
        extern.Face_Init(intern);
        return true;
    }

    maide prusate Bool Final()
    {
        var Extern extern;
        extern : this.Extern;

        var Int intern;
        intern : this.Intern;

        extern.Face_Final(intern);
        extern.Face_Delete(intern);

        this.InternInfra.StringDelete(this.InternFamily);
        return true;
    }

    field prusate String Family { get { return data; } set { data : value; } }
    field prusate Int Size { get { return data; } set { data : value; } }
    field prusate Int Weight { get { return data; } set { data : value; } }
    field prusate Bool Italic { get { return data; } set { data : value; } }
    field prusate Bool Underline { get { return data; } set { data : value; } }
    field prusate Bool Overline { get { return data; } set { data : value; } }
    field prusate Bool Strikeout { get { return data; } set { data : value; } }

    field private Extern Extern { get { return data; } set { data : value; } }
    field private InternInfra InternInfra { get { return data; } set { data : value; } }
    field pronate Int Intern { get { return data; } set { data : value; } }
    field private Int InternFamily { get { return data; } set { data : value; } }
}