class View : Comp
{
    maide prusate Bool Init()
    {
        base.Init();
        this.MathInfra : share MathInfra;
        this.DrawInfra : share DrawInfra;
        
        this.Math : this.CreateMath();
        this.MathComp : this.CreateMathComp();

        this.PosField : this.CreatePosField();
        this.SizeField : this.CreateSizeField();
        this.BackField : this.CreateBackField();
        this.VisibleField : this.CreateVisibleField();
        this.ChildField : this.CreateChildField();

        this.Pos : this.CreatePos();
        this.Size : this.CreateSize();
        this.Back : this.CreateBack();
        this.Visible : true;

        this.Area : this.CreateArea();

        this.DrawRectA : this.CreateDrawRect();
        this.DrawRectB : this.CreateDrawRect();
        this.DrawRectC : this.CreateDrawRect();
        this.DrawRectD : this.CreateDrawRect();

        this.DrawPosA : this.CreateDrawPos();

        this.StackRect : this.CreateStackRect();
        this.StackPos : this.CreateStackPos();
        return true;
    }

    field prusate DrawRect Area { get { return data; } set { data : value; } }

    field precate MathInfra MathInfra { get { return data; } set { data : value; } }
    field precate DrawInfra DrawInfra { get { return data; } set { data : value; } }
    field precate MathMath Math { get { return data; } set { data : value; } }
    field precate MathComp MathComp { get { return data; } set { data : value; } }
    field precate DrawRect DrawRectA { get { return data; } set { data : value; } }
    field precate DrawRect DrawRectB { get { return data; } set { data : value; } }    
    field precate DrawRect DrawRectC { get { return data; } set { data : value; } }
    field precate DrawRect DrawRectD { get { return data; } set { data : value; } }
    field precate DrawPos DrawPosA { get { return data; } set { data : value; } }
    field precate DrawRect StackRect { get { return data; } set { data : value; } }
    field precate DrawPos StackPos { get { return data; } set { data : value; } }

    maide precate MathMath CreateMath()
    {
        var MathMath a;
        a : new MathMath;
        a.Init();
        return a;
    }

    maide precate MathComp CreateMathComp()
    {
        var MathComp a;
        a : new MathComp;
        a.Init();
        return a;
    }

    maide precate Field CreatePosField()
    {
        return this.ViewInfra.FieldCreate(this);
    }

    maide precate Field CreateSizeField()
    {
        return this.ViewInfra.FieldCreate(this);
    }

    maide precate Field CreateBackField()
    {
        return this.ViewInfra.FieldCreate(this);
    }
    
    maide precate Field CreateVisibleField()
    {
        return this.ViewInfra.FieldCreate(this);
    }

    maide precate Field CreateChildField()
    {
        return this.ViewInfra.FieldCreate(this);
    }

    maide precate Pos CreatePos()
    {
        var Pos a;
        a : new Pos;
        a.Init();
        a.Col : 0;
        a.Row : 0;
        return a;
    }

    maide precate Size CreateSize()
    {
        var Size a;
        a : new Size;
        a.Init();
        a.Width : 0;
        a.Height : 0;
        return a;
    }

    maide precate DrawBrush CreateBack()
    {
        var DrawBrush a;
        a : this.DrawInfra.WhiteBrush;
        return a;
    }

    maide precate DrawRect CreateArea()
    {
        var DrawRect rect;
        rect : this.DrawInfra.RectCreate(0, 0, 0, 0);
        return rect;
    }

    maide precate DrawRect CreateDrawRect()
    {
        var DrawRect rect;
        rect : this.DrawInfra.RectCreate(0, 0, 0, 0);
        return rect;
    }

    maide precate DrawPos CreateDrawPos()
    {
        var DrawPos pos;
        pos : this.DrawInfra.PosCreate(0, 0);
        return pos;
    }

    maide precate DrawRect CreateStackRect()
    {
        var DrawRect rect;
        rect : this.DrawInfra.RectCreate(0, 0, 0, 0);
        return rect;
    }

    maide precate DrawPos CreateStackPos()
    {
        var DrawPos pos;
        pos : this.DrawInfra.PosCreate(0, 0);
        return pos;
    }

    field prusate Field PosField { get { return data; } set { data : value; } }

    field prusate Pos Pos
    {
        get
        {
            return cast Pos(this.PosField.Get());
        }
        set
        {
            this.PosField.Set(value);
        }
    }

    maide precate Bool ChangePos(var Change change)
    {
        this.Event(this.PosField);
        return true;
    }

    field prusate Field SizeField { get { return data; } set { data : value; } }

    field prusate Size Size
    {
        get
        {
            return cast Size(this.SizeField.Get());
        }
        set
        {
            this.SizeField.Set(value);
        }
    }

    maide precate Bool ChangeSize(var Change change)
    {
        this.Event(this.SizeField);
        return true;
    }

    field prusate Field BackField { get { return data; } set { data : value; } }

    field prusate DrawBrush Back
    {
        get
        {
            return cast DrawBrush(this.BackField.GetAny());
        }
        set
        {
            this.BackField.SetAny(value);
        }
    }

    maide precate Bool ChangeBack(var Change change)
    {
        this.Event(this.BackField);
        return true;
    }

    field prusate Field VisibleField { get { return data; } set { data : value; } }

    field prusate Bool Visible
    {
        get
        {
            return cast Bool(this.VisibleField.GetAny());
        }
        set
        {
            this.VisibleField.SetAny(value);
        }
    }

    maide precate Bool ChangeVisible(var Change change)
    {
        this.Event(this.VisibleField);
        return true;
    }

    field prusate Field ChildField { get { return data; } set { data : value; } }

    field prusate View Child
    {
        get
        {
            return cast View(this.ChildField.Get());
        }
        set
        {
            this.ChildField.Set(value);
        }
    }

    maide precate Bool ChangeChild(var Change change)
    {
        this.Event(this.ChildField);
        return true;
    }

    maide prusate Bool Change(var Field varField, var Change change)
    {
        inf (this.SizeField = varField)
        {
            this.ChangeSize(change);
        }
        inf (this.PosField = varField)
        {
            this.ChangePos(change);
        }
        inf (this.BackField = varField)
        {
            this.ChangeBack(change);
        }
        inf (this.VisibleField = varField)
        {
            this.ChangeVisible(change);
        }
        inf (this.ChildField = varField)
        {
            this.ChangeChild(change);
        }
        return true;
    }

    maide precate Bool ValidDraw()
    {
        return this.Visible;
    }

    maide prusate Bool ExecuteDraw(var DrawDraw draw)
    {
        this.ViewInfra.AssignDrawRectValue(this.Area, draw.Area);

        inf (!this.ValidDraw())
        {
            return true;
        }
        
        this.ExecuteDrawThis(draw);

        inf (!this.ValidDrawChild())
        {
            return true;
        }

        this.ExecuteDrawChild(draw);
        return true;
    }

    maide precate Bool ExecuteDrawThis(var DrawDraw draw)
    {
        var Int col;
        var Int row;
        col : this.Pos.Col;
        row : this.Pos.Row;
        var Int width;
        var Int height;
        width : this.Size.Width;
        height : this.Size.Height;

        var Int colM;
        var Int rowM;
        colM : this.MathInt(col);
        rowM : this.MathInt(row);

        var DrawRect rect;
        rect : this.DrawRectA;

        rect.Pos.Col : colM;
        rect.Pos.Row : rowM;
        rect.Size.Width : this.MathInt(width);
        rect.Size.Height : this.MathInt(height);

        var DrawBrush brush;
        brush : this.Back;
        draw.Fill : brush;
        
        draw.FillPos.Col : colM;
        draw.FillPos.Row : rowM;
        draw.FillPosSet();

        draw.ExecuteRect(rect);
        
        draw.FillPos.Col : 0;
        draw.FillPos.Row : 0;
        draw.FillPosSet();

        draw.Fill : null;
        return true;
    }

    maide precate Bool ValidDrawChild()
    {
        return !(this.Child = null);
    }

    maide precate Bool ExecuteDrawChild(var DrawDraw draw)
    {
        var Int col;        
        col : this.Pos.Col;
        col : col + draw.Pos.Col;
        var Int row;
        row : this.Pos.Row;
        row : row + draw.Pos.Row;

        var Int width;
        var Int height;
        width : this.Size.Width;
        height : this.Size.Height;

        var DrawRect rect;
        rect : this.DrawRectA;
        rect.Pos.Col : col;
        rect.Pos.Row : row;
        rect.Size.Width : width;
        rect.Size.Height : height;

        this.SetChildArea(rect);

        this.ViewInfra.StackPushChild(draw, this.StackRect, this.StackPos, rect, this.DrawPosA);

        this.ExecuteChildDraw(draw);

        this.ViewInfra.StackPopChild(draw, this.StackRect, this.StackPos);
        return true;
    }

    maide precate Bool SetChildArea(var DrawRect thisRect)
    {
        return true;
    }

    maide precate Bool ExecuteChildDraw(var DrawDraw draw)
    {
        this.Child.ExecuteDraw(draw);
        return true;
    }

    maide precate Int MathInt(var Int n)
    {
        var MathInfra mathInfra;
        mathInfra : this.MathInfra;

        var MathMath math;
        math : this.Math;

        var MathComp mathComp;
        mathComp : this.MathComp;

        var Int a;
        a : mathInfra.Int(math, mathComp, n);
        return a;
    }

    maide precate Int MathValue(var Int cand, var Int expo)
    {
        var MathComp mathComp;
        mathComp : this.MathComp;

        mathComp.Cand : cand;
        mathComp.Expo : expo;

        var Int a;
        a : this.Math.Value(mathComp);
        return a;
    }
}